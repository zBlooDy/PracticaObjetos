import Persona.*
import Enfermedad.*
import EnfermedadAutoinmune.*
import EnfermedadInfecciosa.*

describe "Dr Casa - Temporada 2" {

  const otitis100 = new EnfermedadInfecciosa(cantidadCelulasAmenazadas = 100)
  const malaria500 = new EnfermedadInfecciosa(cantidadCelulasAmenazadas = 500)
  const malaria800 = new EnfermedadInfecciosa(cantidadCelulasAmenazadas = 800)
  const lupus10000 = new EnfermedadAutoinmune(cantidadCelulasAmenazadas = 10000)


  const frank = new Persona(temperatura = 36, cantidadCelulas = 3000000)
  const logan = new Persona(temperatura = 36, cantidadCelulas = 3500000)
  const cameron = new Medico(cantidadDosis = 10, temperatura = 37, cantidadCelulas = 2999000)
  const house = new JefeDepartamento(temperatura = 37, cantidadCelulas = 3000000)
  
  method initialize() {
    frank.contraerEnfermedad(malaria800)
    
    logan.contraerEnfermedad(malaria500)
    logan.contraerEnfermedad(otitis100)
    logan.contraerEnfermedad(lupus10000)

    house.subordinar(cameron)

  }

  test "1.a Enfermedad Infecciosa se atenua correctamente" {
    malaria500.atenuarse(100)

    assert.equals(400, malaria500.cantidadCelulasAmenazadas())
  }

  test "1.b Enfermedad Autoinmune se atenua correctamente" {
    lupus10000.atenuarse(500)
    
    assert.equals(9500, lupus10000.cantidadCelulasAmenazadas())
  }

  test "2.a Cameron atiende a Logan y atenua malaria500" {
    
    cameron.atenderA(logan) 

    assert.equals(350, malaria500.cantidadCelulasAmenazadas())
  }

  test "2.b Cameron atiende a Logan y cura otitis" {
    
    cameron.atenderA(logan) 

    assert.notThat(logan.tiene(otitis100))
  }

  test "3.a House atiende a Cameron" {

    house.atenderA(cameron)

    cameron.contraerEnfermedad(malaria800)

    assert.equals(650, malaria800.cantidadCelulasAmenazadas())

  }
  test "3.b House atiende a Logan y cura otitis" {

    house.subordinar(cameron)

    house.atenderA(logan)

    assert.notThat(logan.tiene(otitis100))
  }

  test "4.a Cameron contrae malaria (al ser medico se autoatiende)" {

    cameron.contraerEnfermedad(malaria500)
    
    assert.that(cameron.tiene(malaria500))

    assert.equals(350, malaria500.cantidadCelulasAmenazadas())

  }

  test "4.b House contrae malaria (al ser medico se autoatiende)" {

    house.contraerEnfermedad(malaria500)
    
    assert.that(house.tiene(malaria500))

    assert.equals(350, malaria500.cantidadCelulasAmenazadas())

  }

  test "5. House contrae la muerte" {
    
    house.contraerEnfermedad(laMuerte)

    house.vivirUnDia()

    assert.equals(0, house.temperatura())
  }


}